cmake_minimum_required(VERSION 3.0.2)
project(tracy_catkin)

# Read what Tracy version should be built from the TRACY_VERSION file
file(STRINGS "TRACY_VERSION" TRACY_VERSION)

# Tell CMAKE to rebuild when the TRACY_VERSION variable changes
set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS TRACY_VERSION)

# Initialize catkin
find_package(catkin_simple REQUIRED)
catkin_simple()

# Include compiler options and definitions
include(cmake/tracy_catkin-extras.cmake)

# Create the include directory to install the headers, if needed
file(MAKE_DIRECTORY ${CATKIN_DEVEL_PREFIX}/include)

# Download and build Tracy sources
include(ExternalProject)
ExternalProject_Add(
    ${PROJECT_NAME}_src
    GIT_REPOSITORY https://github.com/wolfpld/tracy.git
    GIT_TAG ${TRACY_VERSION}
    GIT_SHALLOW TRUE
    GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
    CONFIGURE_COMMAND cd ../${PROJECT_NAME}_src &&
    cmake .
    -DCMAKE_INSTALL_PREFIX:PATH=${CATKIN_DEVEL_PREFIX}
    -DBUILD_SHARED_LIBS:BOOL=true
    -DCMAKE_BUILD_TYPE:STRING=Release
    -DCMAKE_TOOLCHAIN_FILE:STRING=${CMAKE_TOOLCHAIN_FILE}
    # Pass all Tracy options
    -DTRACY_ENABLE:BOOL=${TRACY_ENABLE}
    -DTRACY_ON_DEMAND:BOOL=${TRACY_ON_DEMAND}
    -DTRACY_CALLSTACK:BOOL=${TRACY_CALLSTACK}
    -DTRACY_NO_CALLSTACK:BOOL=${TRACY_NO_CALLSTACK}
    -DTRACY_NO_CALLSTACK_INLINES:BOOL=${TRACY_NO_CALLSTACK_INLINES}
    -DTRACY_ONLY_LOCALHOST:BOOL=${TRACY_ONLY_LOCALHOST}
    -DTRACY_NO_BROADCAST:BOOL=${TRACY_NO_BROADCAST}
    -DTRACY_ONLY_IPV4:BOOL=${TRACY_ONLY_IPV4}
    -DTRACY_NO_CODE_TRANSFER:BOOL=${TRACY_NO_CODE_TRANSFER}
    -DTRACY_NO_CONTEXT_SWITCH:BOOL=${TRACY_NO_CONTEXT_SWITCH}
    -DTRACY_NO_EXIT:BOOL=${TRACY_NO_EXIT}
    -DTRACY_NO_SAMPLING:BOOL=${TRACY_NO_SAMPLING}
    -DTRACY_NO_VERIFY:BOOL=${TRACY_NO_VERIFY}
    -DTRACY_NO_VSYNC_CAPTURE:BOOL=${TRACY_NO_VSYNC_CAPTURE}
    -DTRACY_NO_FRAME_IMAGE:BOOL=${TRACY_NO_FRAME_IMAGE}
    -DTRACY_NO_SYSTEM_TRACING:BOOL=${TRACY_NO_SYSTEM_TRACING}
    -DTRACY_DELAYED_INIT:BOOL=${TRACY_DELAYED_INIT}
    -DTRACY_MANUAL_LIFETIME:BOOL=${TRACY_MANUAL_LIFETIME}
    -DTRACY_FIBERS:BOOL=${TRACY_FIBERS}
    -DTRACY_NO_CRASH_HANDLER:BOOL=${TRACY_NO_CRASH_HANDLER}
    -DTRACY_TIMER_FALLBACK:BOOL=${TRACY_TIMER_FALLBACK}
    BUILD_COMMAND cd ../${PROJECT_NAME}_src && make -j 8
    INSTALL_COMMAND cd ../${PROJECT_NAME}_src && make install -j 8
)

# Install Tracy's headers
install(DIRECTORY ${CATKIN_DEVEL_PREFIX}/include/common
    DESTINATION ${CATKIN_GLOBAL_INCLUDE_DESTINATION}
    FILES_MATCHING PATTERN "*.h" PATTERN "*.hpp")
install(DIRECTORY ${CATKIN_DEVEL_PREFIX}/include/client
    DESTINATION ${CATKIN_GLOBAL_INCLUDE_DESTINATION}
    FILES_MATCHING PATTERN "*.h" PATTERN "*.hpp")
install(DIRECTORY ${CATKIN_DEVEL_PREFIX}/include/tracy
    DESTINATION ${CATKIN_GLOBAL_INCLUDE_DESTINATION}
    FILES_MATCHING PATTERN "*.h" PATTERN "*.hpp")

# Install Tracy's library
install(DIRECTORY ${CATKIN_DEVEL_PREFIX}/lib/
    DESTINATION ${CATKIN_PACKAGE_LIB_DESTINATION}
    FILES_MATCHING PATTERN "libTracyClient*")

# Make Tracy available to other catkin packages
cs_export(INCLUDE_DIRS ${CATKIN_DEVEL_PREFIX}/include
    LIBRARIES TracyClient
    CFG_EXTRAS tracy_catkin-extras.cmake)
